// B has input A, noise(64x64, repeat)
void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
	vec2 uv = fragCoord.xy / iResolution.xy;
    vec4 col = texture(iChannel0, uv);
    vec2 step = 1. / iResolution.xy;
    
    vec4 n = texture( iChannel0, uv + step * vec2( 0., 1.) );
    vec4 s = texture( iChannel0, uv + step * vec2( 0., -1.) );
    vec4 e = texture( iChannel0, uv + step * vec2( 1., 0.) );
    vec4 w = texture( iChannel0, uv + step * vec2( -1., 0.) );

    // water logic
    // https://web.archive.org/web/20080618181901/http://freespace.virgin.net/hugo.elias/graphics/x_water.htm
	float viscosityConstant = 0.997;
    float damping = 0.997;
    float newHeight = ( ( n.r + s.r + e.r + w.r ) * 0.5 - col.g ) * viscosityConstant;
	newHeight *= damping;
    
    //add mouse effect
    float mouseDist = length(iMouse.xy / iResolution.xy - uv);
    if (mouseDist < length(step*7.) && iMouse.z > 0.) {
    	newHeight += 0.006 + 0.002 * sin(iTime) + 0.001 * sin(fragCoord.x*0.3 + fragCoord.y*0.05);
    }
    newHeight = clamp(newHeight, 0., 1.);
    
    
    // add noise
    float noiseUvScale = 0.2;
    float noise1 = texture( iChannel1, uv * noiseUvScale + vec2(0.02, 0.02)*iTime).r;
    float noise2 = texture( iChannel1, uv * noiseUvScale*0.4 - vec2(0.011, 0.05) * iTime).r;
    float noise3 = texture( iChannel1, uv * noiseUvScale*1.3 + vec2(0.035, -0.04) * iTime).r;
    float noiseCombo = (noise1 + noise2 + noise3) / 6.;
    newHeight += noiseCombo * 0.0002;
    
    // simulate buffer swapping with channels r,g
    vec3 finalColor = vec3(0.);
    finalColor.g = col.r;
    finalColor.r = newHeight;
    fragColor = vec4(finalColor, 1.);
}